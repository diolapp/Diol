/*
 *  Copyright (C) 2019  The Diol App Team
 *
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 28
    signingConfigs {
        bitrise {
            storeFile file(System.getenv("HOME") + "/keystores/diol_keystore.jks")
            storePassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PASSWORD")
            keyAlias System.getenv("BITRISEIO_ANDROID_KEYSTORE_ALIAS")
            keyPassword System.getenv("BITRISEIO_ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD")
        }
    }
    defaultConfig {
        applicationId "app.diol"
        minSdkVersion 23
        targetSdkVersion 28

        if (System.getenv("BITRISE_IO") == "true") {
            versionCode = Integer.valueOf(System.getenv("BITRISE_BUILD_NUMBER"))
            versionName = "0.0.1-alpha"
        } else {
            versionCode 1
            versionName "dev"
        }

        packagingOptions {
            exclude 'META-INF/DEPENDENCIES'
            exclude 'META-INF/NOTICE'
            exclude 'META-INF/LICENSE'
            exclude 'META-INF/LICENSE.txt'
            exclude 'META-INF/NOTICE.txt'
        }
    }
    buildTypes {
        debug {
            if (System.getenv("BITRISE_IO") == "true") {
                minifyEnabled true
                shrinkResources true

                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                if (System.getenv("BITRISE_PULL_REQUEST") == null) {
                    signingConfig signingConfigs.bitrise
                }
            } else {
                minifyEnabled false
            }
        }

        release {
            applicationIdSuffix ".dialer"

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (System.getenv("BITRISE_PULL_REQUEST") == null) {
                signingConfig signingConfigs.bitrise
            }
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            proto {
                srcDir 'src/main/proto'
                include '**/*.proto'
            }
        }
    }
    buildToolsVersion '28.0.3'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.7.0'
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

afterEvaluate {
    android.applicationVariants.all { variant ->
        variant.resValue "string", "application_id", variant.applicationId
    }
}

dependencies {
    // Implementations
    //noinspection GradleCompatible
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:exifinterface:28.0.0'
    implementation 'com.android.support:support-compat:28.0.0'
    implementation 'com.android.support:preference-v7:28.0.0'
    implementation 'com.android.support:support-v13:28.0.0'
    implementation 'com.android.support:viewpager:28.0.0'
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.google.android.gms:play-services-location:16.0.0'
    implementation 'com.google.android.gms:play-services-maps:16.1.0'
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'
    implementation 'com.squareup.okhttp3:okhttp:3.14.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'me.leolin:ShortcutBadger:1.1.22@aar'

    // APIs
    api 'com.android.volley:volley:1.1.1'
    api 'com.googlecode.libphonenumber:libphonenumber:8.10.7'
    api 'com.googlecode.libphonenumber:geocoder:2.113'
    api 'com.google.auto.value:auto-value-annotations:1.6.2'
    api 'com.google.auto:auto-common:0.10'
    api 'com.google.code.findbugs:jsr305:3.0.2'
    api 'com.google.dagger:dagger:2.21'
    api 'com.google.guava:guava:27.1-android'
    api 'com.google.zxing:core:3.3.3'
    //noinspection GradleDependency
    api 'commons-io:commons-io:2.6'
    api 'io.grpc:grpc-context:1.19.0'
    api 'io.grpc:grpc-core:1.19.0'
    api 'io.grpc:grpc-okhttp:1.19.0'
    api 'io.grpc:grpc-protobuf-lite:1.19.0'
    api 'io.grpc:grpc-stub:1.19.0'
    api 'org.apache.james:apache-mime4j-core:0.8.3'
    api 'org.apache.james:apache-mime4j-dom:0.8.3'

    // Annotation Processors
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'
    annotationProcessor 'com.google.auto.value:auto-value:1.6.2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.21'
}

repositories {
    mavenCentral()
}
